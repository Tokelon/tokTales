// tokTales-master build.gradle
group "com.tokelon.toktales"
version "0.2.1"


buildscript {
	repositories {
		google()
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:4.0.1'

		// TODO: Replace with 'maven-publish' plugin, as it supports Android by now
		classpath 'digital.wup:android-maven-publish:3.6.3'

		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'

		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.18.0"
	}
}

allprojects {
	repositories {
		google()
		jcenter()
	}

	apply plugin: 'eclipse'

	apply plugin: "com.jfrog.artifactory"

	task printVersion {
		doLast {
			println project.version
		}
	}
}


task wrapperCustom(type: Wrapper) {
	group 'build setup'
	gradleVersion = '6.1.1'
	distributionType = Wrapper.DistributionType.ALL
}



// Apply Java plugin to enable Javadoc task
apply plugin: 'java'

task javadocAggregated(type: Javadoc, description: 'Generate Javadoc from all subprojects as if it was a single project.', group: 'documentation') {
	destinationDir = file("$buildDir/docs/javadoc")
	title = "tokTales ${project.version + (project.hasProperty("bintray.version") ? "" : ("-" + (project.findProperty("buildNumber") ?: "SNAPSHOT")))} API"
	options.links (
			'https://docs.oracle.com/javase/8/docs/api/',
			'https://developer.android.com/reference',
			'https://google.github.io/guice/api-docs/4.2.3/javadoc/',
			'https://guava.dev/releases/29.0-android/api/docs/'
	)
	options.addBooleanOption('Xdoclint:none', true)

	gradle.projectsEvaluated({
		subprojects.each { project ->
			project.tasks.withType(Javadoc).each { javadocTask ->
				source += javadocTask.source
				classpath += javadocTask.classpath
				excludes += javadocTask.excludes
				includes += javadocTask.includes
			}
		}
	})
}


def pomConfig = {
	name = 'tokTales Engine'
	description = 'Part of tokTales Engine, a neat game engine.'
	url = 'https://github.com/Tokelon/tokTales'
	licenses {
		license {
			name = 'MIT License'
			url = 'https://opensource.org/licenses/MIT'
		}
	}
	developers {
		developer {
			id = 'Tokelon'
			name = 'Elias Paralikes'
			email = 'tokelontales@gmail.com'
		}
	}
	scm {
		url = 'https://github.com/Tokelon/tokTales'
		connection = 'scm:git:https://github.com/Tokelon/tokTales.git'
		developerConnection = 'scm:git:https://github.com/Tokelon/tokTales.git'
	}
	issueManagement {
		system = 'GitHub Issues'
		url = 'https://github.com/Tokelon/tokTales/issues'
	}
}

subprojects { -> project
	apply plugin: 'com.jfrog.bintray'

	// Disable publish task which we don't need, because it throws an exception (probably related to the root project)
	bintrayPublish.enabled = false

	plugins.withType(JavaLibraryPlugin) {
		task sourcesJar(type: Jar, dependsOn: classes) {
			classifier = 'sources'
			from sourceSets.main.allSource
		}

		project.javadoc {
			//failOnError = false
			options.addBooleanOption('Xdoclint:none', true)
		}

		task javadocJar(type: Jar, dependsOn: javadoc) {
			classifier = 'javadoc'
			from javadoc.destinationDir
		}

		artifacts {
			archives sourcesJar
			archives javadocJar
		}


		project.apply plugin: 'maven-publish'

		publishing {
			repositories {
				maven {
					name = "GitHubPackages"
					url = uri("https://maven.pkg.github.com/" + (project.findProperty("gpr.repo") ?: System.getenv("GITHUB_REPOSITORY")))
					credentials {
						username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
						password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
					}
				}
			}
			publications {
				maven(MavenPublication) {
					version project.version + (project.hasProperty("bintray.version") ? "" : ("-" + (project.findProperty("buildNumber") ?: "SNAPSHOT")))
					from(components.java)
					artifact sourcesJar
					artifact javadocJar

					pom pomConfig
				}
			}
		}

		bintray {
			user = project.findProperty('bintray.user')
			key = project.findProperty('bintray.key')
			publications = ['maven']
			publish = false
			override = false
			dryRun = false

			pkg {
				repo = 'tokTales'
				name = 'tokTales'
				desc = 'A neat game engine.'
				licenses = ['MIT']
				websiteUrl = 'https://github.com/Tokelon/tokTales'
				issueTrackerUrl = 'https://github.com/Tokelon/tokTales/issues'
				vcsUrl = 'https://github.com/Tokelon/tokTales.git'
				githubRepo = 'Tokelon/tokTales'
				version {
					name = project.version
					desc = project.version
					released = new Date()
				}
			}
		}

		artifactory {
			contextUrl = "http://oss.jfrog.org"
			publish {
				repository {
					repoKey = 'oss-snapshot-local'
					username = project.findProperty('bintray.user')
					password = project.findProperty('bintray.key')
					maven = true
				}
				defaults {
					publications('maven')

					publishBuildInfo = true
					publishArtifacts = true
					publishPom = true
				}
			}
			resolve {
				repository {
					repoKey = 'jcenter'
				}
			}

			clientConfig.info.setBuildNumber(project.findProperty('artifactory.buildNumber') ?: clientConfig.info.getBuildNumber())
		}
	}

	plugins.withType(com.android.build.gradle.LibraryPlugin) {
		afterEvaluate {
			task sourcesJar(type: Jar) {
				from android.sourceSets.main.java.srcDirs
				classifier = 'sources'
			}

			task javadoc(type: Javadoc, dependsOn: 'assembleRelease') {
				//failOnError false
				options.addBooleanOption('Xdoclint:none', true)

				source = android.sourceSets.main.java.srcDirs
				classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
				android.libraryVariants.all { variant ->
					if (variant.name == 'release') {
						owner.classpath += variant.javaCompileProvider.get().classpath
					}
				}
			}

			task javadocJar(type: Jar, dependsOn: javadoc) {
				classifier = 'javadoc'
				from javadoc.destinationDir
			}

			artifacts {
				archives javadocJar
				archives sourcesJar
			}

			bintray {
				user = project.findProperty('bintray.user')
				key = project.findProperty('bintray.key')
				publications = ['maven']
				publish = false
				override = false
				dryRun = false

				pkg {
					repo = 'tokTales'
					name = 'tokTales'
					desc = 'A neat game engine.'
					licenses = ['MIT']
					websiteUrl = 'https://github.com/Tokelon/tokTales'
					issueTrackerUrl = 'https://github.com/Tokelon/tokTales/issues'
					vcsUrl = 'https://github.com/Tokelon/tokTales.git'
					githubRepo = 'Tokelon/tokTales'
					version {
						name = project.version
						desc = project.version
						released = new Date()
					}
				}
			}

			artifactory {
				contextUrl = "http://oss.jfrog.org"
				publish {
					repository {
						repoKey = 'oss-snapshot-local'
						username = project.findProperty('bintray.user')
						password = project.findProperty('bintray.key')
						maven = true
					}
					defaults {
						publications('maven')

						publishBuildInfo = true
						publishArtifacts = true
						publishPom = true
					}
				}
				resolve {
					repository {
						repoKey = 'jcenter'
					}
				}

				clientConfig.info.setBuildNumber(project.findProperty('artifactory.buildNumber') ?: clientConfig.info.getBuildNumber())
			}
		}


		project.apply plugin: 'digital.wup.android-maven-publish'

		publishing {
			repositories {
				maven {
					name = "GitHubPackages"
					url = uri("https://maven.pkg.github.com/" + (project.findProperty("gpr.repo") ?: System.getenv("GITHUB_REPOSITORY")))
					credentials {
						username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
						password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
					}
				}
			}
			publications {
				maven(MavenPublication) {
					version project.version + (project.hasProperty("bintray.version") ? "" : ("-" + (project.findProperty("buildNumber") ?: "SNAPSHOT")))
					from components.android

					pom pomConfig
				}
			}
		}
	}
}
