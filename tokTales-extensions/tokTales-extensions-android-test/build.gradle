// tokTales-extensions-android-test build.gradle
group "com.tokelon.toktales"
version "0.0.1"


apply plugin: 'com.android.library'

android {
	compileSdkVersion 28
	
	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 28
		versionCode 1
		versionName "0.0.1"
		
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}
	
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}


dependencies {
	api project(':tokTales-android-test')
	
	// Cyclic dependency, see tokTales-core-library build.gradle
	implementation project(':tokTales-extensions-default-android')
	
	api (project(':tokTales-extensions-core-test')) {
		exclude group: 'xmlpull', module: 'xmlpull'
		exclude group: 'xpp3', module: 'xpp3_min'
	}
}


// Only for eclipse
eclipse.classpath.file.withXml { xml ->
	def node = xml.asNode()
	
	node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-android-test' ])
	node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-extensions-core-test' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'src', path: '/tokTales-extensions-default-android' ]) // Cyclic dependency
	
	
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-appcompat-v7-classes.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-classes.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-internal.jar' ])
	if(node.children().find( {it.@path == "org.eclipse.jdt.junit.JUNIT_CONTAINER/4"} ) == null) {
		node.appendNode('classpathentry', [ exported: false, kind: 'con', path: 'org.eclipse.jdt.junit.JUNIT_CONTAINER/4' ])
	}
}

// Only for eclipse
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			
			srcDir '/build/generated/source/r/debug'
			srcDir '/build/generated/not_namespaced_r_class_sources/debug/generateDebugRFile/out'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
		}
	}
}
