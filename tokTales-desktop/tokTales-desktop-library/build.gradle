// tokTales-desktop-library build.gradle
group "com.tokelon.toktales"
version "0.0.1"


// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'


sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			
			srcDir 'exp/main/java'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
			
			srcDir 'exp/test/java'
		}
	}
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"



// LWJGL configuration
project.ext.lwjglVersion = "3.1.6"

import org.gradle.internal.os.OperatingSystem

switch ( OperatingSystem.current() ) {
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
		break
}


dependencies {
	/* Test dependencies */
	testImplementation project(':tokTales-desktop-test')
	
	
	/* Project dependencies */
	api project(':tokTales-tools-desktop')
	
	api (project(':tokTales-core-library')) {
		// This is necessary because otherwise projects depending on this, might get the android version (if the version is newer)
		exclude group: 'com.google.guava', module: 'guava'
	}
	api ('com.google.guava:guava:26.0-jre') {
		// Force override transient depdencies from Guice and tokTales-core-library with the version we want
		// Whether we have to exclude and force depends on whether the overridden version is newer
		force true
	}
	
	
	/* Lib dependencies */
	
	// LWJGL
	api "org.lwjgl:lwjgl:$lwjglVersion"
	api "org.lwjgl:lwjgl-assimp:$lwjglVersion" // Include assimp?
	api "org.lwjgl:lwjgl-glfw:$lwjglVersion"
	api "org.lwjgl:lwjgl-openal:$lwjglVersion"
	api "org.lwjgl:lwjgl-opengl:$lwjglVersion"
	api "org.lwjgl:lwjgl-stb:$lwjglVersion"
	api "org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives"
	api "org.lwjgl:lwjgl-assimp:$lwjglVersion:$lwjglNatives"
	api "org.lwjgl:lwjgl-glfw:$lwjglVersion:$lwjglNatives"
	api "org.lwjgl:lwjgl-openal:$lwjglVersion:$lwjglNatives"
	api "org.lwjgl:lwjgl-opengl:$lwjglVersion:$lwjglNatives"
	api "org.lwjgl:lwjgl-stb:$lwjglVersion:$lwjglNatives"
}
