// tokTales-core build.gradle
group "com.tokelon.toktales"
version "0.0.6"


// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'

apply plugin: 'eclipse'

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'tools/main/java'

			srcDir 'exp/main/java'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
			srcDir 'tools/test/java'
		}
	}
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"


dependencies {
	/* This causes cyclic dependency errors in Eclipse.
	 * Eclipse, as opposed to Gradle, does not understand that the dependency is only for the tests.
	 *
	 * The current workaround is to change the error to a warning, as described here:
	 * https://github.com/eclipse/buildship/issues/460
	 */
	testImplementation project(':tokTales-test-core')


	// LuaJ
	implementation 'org.luaj:luaj-jse:3.0.1'
	
	// XStream
	implementation ('com.thoughtworks.xstream:xstream:1.4.8') {
		/* We need to exclude these because of duplicate class XmlPullParserException that Android includes in various configurations
		 * The only way to fix it for Android is to include the following in ALL Android projects, and we're not doing that
		 configurations {
           //all*.exclude group: 'xmlpull', module:'xmlpull'
           //all*.exclude group: 'xpp3', module:'xpp3_min'
         } */
		exclude group: 'xmlpull', module: 'xmlpull'
		exclude group: 'xpp3', module: 'xpp3_min'
	}
	
	
	// Guice
    api 'com.google.inject:guice:4.1.0:no_aop'
    // extensions require aop version, disable with transitive false
    api ('com.google.inject.extensions:guice-assistedinject:4.1.0') {
		transitive = false
	}
	api ('com.google.inject.extensions:guice-multibindings:4.1.0') {
    	transitive = false
    }
    
    /* JOML
	 * Updating to >= v1.8.0 is problematic for two reasons:
     * 1. Compatibility with Android (> v1.8.6) is questionable - it works,
     * but compiles with lots of warnings / other versions do not compile at all
     * 2. On Desktop it always crashes immediately because of STBTruetype.stbtt_GetCodepointBitmap,
     * this will have to be fixed anyway but not right now.
     *
     * This version might be preferable anyways,
     * no natives, no weird backporting for Java 1.2/1.3 etc.
     */
	//api 'org.joml:joml:1.8.0'
	api 'org.joml:joml:1.8.6'
	
	// Streamsupport
	api 'net.sourceforge.streamsupport:android-retrostreams:1.6.3'
	api 'net.sourceforge.streamsupport:android-retrofuture:1.6.3'
	//api 'net.sourceforge.streamsupport:android-retroatomic:1.6.3'
	//api 'net.sourceforge.streamsupport:android-retroflow:1.6.3'
	
}


// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}


// Export project and dependencies for Android
eclipse.classpath.file.withXml { xml ->
    def node = xml.asNode()
    
    def con = node.children().find( {it.@path == "org.eclipse.buildship.core.gradleclasspathcontainer"} )
	if(con) {
		con.@exported = 'true'
	}
}

task wrapper(type: Wrapper) {
	group 'build setup'
	gradleVersion = '4.6'
	distributionType = Wrapper.DistributionType.ALL
}
