// tokTales-core-library build.gradle
group "com.tokelon.toktales"
version "0.2.1-rc1"


buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:2.0.0"
	}
}


// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'

apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: "com.github.spotbugs"


sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			
			srcDir 'exp/main/java'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
			
			srcDir 'exp/test/java'
		}
	}
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"


dependencies {
	/* This causes cyclic dependency errors in Eclipse.
	 * Eclipse, as opposed to Gradle, does not understand that the dependency is only for the tests.
	 *
	 * The current workaround is to change the error to a warning, as described here:
	 * https://github.com/eclipse/buildship/issues/460
	 */
	testImplementation project(':tokTales-core-test')
	
	// Tools
	api project(':tokTales-tools-core')
	api project(':tokTales-tools-script')
	api project(':tokTales-tools-assets')
	api project(':tokTales-tools-core-inject')
	api (project(':tokTales-tools-tiled')) {
		// Exclude these because of duplicate classes that Android already contains
		// Include where needed by redeclaring XStream dependency (e.g. Desktop)
		exclude group: 'xmlpull', module: 'xmlpull'
		exclude group: 'xpp3', module: 'xpp3_min'
	}

	
	
	// Guice
	api 'com.google.inject:guice:4.2.3:no_aop'
	// We have to use an older version for this library because the newer one breaks for older Android versions,
	// since it references Java 8 APIs unsupported before Android API 26.
	api ('com.google.inject.extensions:guice-assistedinject:4.2.2') {
		// We need to force this version otherwise it will be overridden by the main Guice dependency
		force true
		// Exclude these so we can use our own versions (no-AOP for Guice and android for Guava)
		exclude group: 'com.google.inject', module: 'guice'
		exclude group: 'com.google.guava', module: 'guava'
	}

	// Guava
	api 'com.google.guava:guava:29.0-android' // This will override the version provided by Guice
	
	
	// JOML
	//api 'org.joml:joml:1.8.6' // previous version
	api 'org.joml:joml:1.9.25'
	
	// Streamsupport
	api 'net.sourceforge.streamsupport:android-retrostreams:1.7.2'
	api 'net.sourceforge.streamsupport:android-retrofuture:1.7.2'
	//api 'net.sourceforge.streamsupport:android-retroatomic:1.7.2'
	//api 'net.sourceforge.streamsupport:android-retroflow:1.7.2'
	
	// Trove
	api 'net.sf.trove4j:trove4j:3.0.3'
	
	
	// SLF4J API - No binding
	api 'org.slf4j:slf4j-api:1.7.30'
	
	// SLF4J - sysout-over-slf4j (https://projects.lidalia.org.uk/sysout-over-slf4j)[MIT License]
	implementation 'uk.org.lidalia:sysout-over-slf4j:1.0.2'
}


// Export project and dependencies for Android
eclipse.classpath.file.withXml { xml ->
	def node = xml.asNode()
	
	def con = node.children().find( {it.@path == "org.eclipse.buildship.core.gradleclasspathcontainer"} )
	if(con) {
		con.@exported = 'true'
	}
}


checkstyle {
	toolVersion = '8.23' // important - old versions might not support config
	ignoreFailures = true
	
	configFile rootProject.file('../tokTales-master/config/checkstyle/checkstyle.xml') // adjust if needed
	
	/* Remove * to enable Checkstyle */
	checkstyleMain.enabled = false
	checkstyleTest.enabled = false
	/**/
}

pmd {
	toolVersion = '6.17.0' // important - old versions might not support config
	ignoreFailures = true
	
	//ruleSetConfig = resources.text.fromFile(rootProject.file('../tokTales-master/config/pmd/pmd.xml'))
	ruleSetFiles = files('../../tokTales-master/config/pmd/pmd.xml')
	
	/* Remove * to enable PMD */
	pmdMain.enabled = false
	pmdTest.enabled = false
	/**/
}

spotbugs {
	toolVersion = '3.1.12' // important - old versions might not support config
	ignoreFailures = true
	
	/* Remove * to enable Spotbugs */
	spotbugsMain.enabled = false
	spotbugsTest.enabled = false
	/**/
}
