// tokTales-android-test build.gradle
group "com.tokelon.toktales"
version "0.0.1"


apply plugin: 'com.android.library'

android {
	compileSdkVersion 28
	
	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 28
		versionCode 1
		versionName "0.0.1"
		
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}
	
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}


dependencies {
	api 'com.android.support.test:runner:1.0.2'
	api 'com.android.support.test.espresso:espresso-core:3.0.2'
	
	// Mockito for Android instrumentation tests
	api "org.mockito:mockito-android:2.15.0" // Should be androidTestApi but needs to be exported
	
	
	implementation 'com.android.support:appcompat-v7:28.0.0'
	implementation 'com.android.support:support-v4:28.0.0'
	
	api (project(':tokTales-core-test')) {
		exclude group: 'xmlpull', module: 'xmlpull'
		exclude group: 'xpp3', module: 'xpp3_min'
		exclude group: 'com.google.inject', module: 'guice'
	}
	api ('com.google.inject:guice:4.2.1:no_aop') {
		force = true
	}
	
	// Cyclic dependency, see tokTales-core-library build.gradle
	implementation project(':tokTales-android-library')
	
	// WORKAROUND - When running unit tests for Android, only AndroidStudio fails to include this dependency
	api 'org.luaj:luaj-jse:3.0.1'
}


// Only for eclipse
eclipse.classpath.file.withXml { xml ->
	def node = xml.asNode()
	
	node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-core-test' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'src', path: '/tokTales-android-library' ]) // Cyclic dependency
	
	
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-appcompat-v7-classes.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-classes.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-internal.jar' ])
	if(node.children().find( {it.@path == "org.eclipse.jdt.junit.JUNIT_CONTAINER/4"} ) == null) {
		node.appendNode('classpathentry', [ exported: false, kind: 'con', path: 'org.eclipse.jdt.junit.JUNIT_CONTAINER/4' ])
	}
}

// Only for eclipse
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			
			srcDir '/build/generated/source/r/debug'
			srcDir '/build/generated/not_namespaced_r_class_sources/debug/generateDebugRFile/out'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
		}
	}
}
