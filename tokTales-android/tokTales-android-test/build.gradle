// tokTales-android-test build.gradle
group "com.tokelon.toktales"
version "0.2.0"


apply plugin: 'com.android.library'

android {
	compileSdkVersion 30
	
	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 30
		versionCode 20004
		versionName "0.2.0"
		
		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
	}
	
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	
	compileOptions {
		coreLibraryDesugaringEnabled true

		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}


dependencies {
	api 'androidx.test.ext:junit:1.1.2'
	api 'androidx.test.espresso:espresso-core:3.3.0'
	api 'androidx.test:runner:1.3.0'

	// Mocking for tests
	api 'org.mockito:mockito-core:3.5.10'
	//androidTestImplementation 'org.mockito:mockito-android:3.5.10' // Needed for instrumented tests? / Won't be exported


	coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'androidx.legacy:legacy-support-v4:1.0.0'
	
	api project(':tokTales-core-test')

	// Cyclic dependency, see tokTales-core-library build.gradle
	implementation project(':tokTales-android-library')
}


// Only for eclipse
eclipse.classpath.file.withXml { xml ->
	def node = xml.asNode()
	
	node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-core-test' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'src', path: '/tokTales-android-library' ]) // Cyclic dependency
	
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android-test/build/intermediates/compile_only_not_namespaced_r_class_jar/debug/generateDebugRFile/R.jar' ])
	
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-appcompat-v7-classes.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-classes.jar' ])
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-internal.jar' ])
	if(node.children().find( {it.@path == "org.eclipse.jdt.junit.JUNIT_CONTAINER/4"} ) == null) {
		node.appendNode('classpathentry', [ exported: false, kind: 'con', path: 'org.eclipse.jdt.junit.JUNIT_CONTAINER/4' ])
	}
}

// Only for eclipse
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			
			srcDir 'exp/main/java'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
			
			srcDir 'exp/test/java'
		}
	}
}
