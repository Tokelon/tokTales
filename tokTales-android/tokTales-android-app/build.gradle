// tokTales-android-app build.gradle
group "com.tokelon.toktales"
version "0.0.1"


apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.tokelon.toktales.tokteller"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES' // Exclude duplicate files from Guice
    }
}


dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
	
	testImplementation project(':tokTales-test-android')
	androidTestImplementation project(':tokTales-test-android')
	
	
	implementation 'com.android.support:appcompat-v7:28.0.0'
	implementation 'com.android.support:support-v4:28.0.0'
	
	implementation project(':tokTales-android-library')
}



// Only for eclipse
eclipse.classpath.file.withXml { xml ->
    def node = xml.asNode()
    
    node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-android-library' ])
    
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-appcompat-v7-classes.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-classes.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-internal.jar' ])
    if(node.children().find( {it.@path == "org.eclipse.jdt.junit.JUNIT_CONTAINER/4"} ) == null) {
    	node.appendNode('classpathentry', [ exported: false, kind: 'con', path: 'org.eclipse.jdt.junit.JUNIT_CONTAINER/4' ])
    }
}

// Only for eclipse
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			
			srcDir '/build/generated/source/r/debug'
			srcDir '/build/generated/not_namespaced_r_class_sources/debug/generateDebugRFile/out'
			srcDir '/build/generated/not_namespaced_r_class_sources/debug/processDebugResources/r'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
		}
	}
}
