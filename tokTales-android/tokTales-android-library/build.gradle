// tokTales-android-library build.gradle
group "com.tokelon.toktales"
version "0.2.1"


apply plugin: 'com.android.library'

android {
	compileSdkVersion 30

	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 30
		versionCode 20101
		versionName "0.2.1"

		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		coreLibraryDesugaringEnabled true

		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	packagingOptions {
		exclude 'META-INF/DEPENDENCIES' // Exclude duplicate files from Guice
	}
}


dependencies {
	testImplementation project(':tokTales-android-test')
	androidTestImplementation project(':tokTales-android-test')

	// Mockito Android needed for mocking in instrumentation tests
	androidTestImplementation 'org.mockito:mockito-android:3.5.13'


	coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'androidx.legacy:legacy-support-v4:1.0.0'

	api project(':tokTales-tools-android')

	api project(':tokTales-core-library')
}


// Tell Gradle eclipse plugin to add dependencies to classpath
eclipse.classpath.file.withXml { xml ->
	def node = xml.asNode()

	node.appendNode('classpathentry', [ exported: false, kind: 'src', path: '/tokTales-android-test' ])

	node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-tools-android' ])
	node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-core-library' ])


	// Export for dependent projects?
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android-library/build/intermediates/compile_only_not_namespaced_r_class_jar/debug/R.jar' ])

	// for some reason there can be a second entry sometimes
	if(node.children().find( {it.@path == "org.eclipse.jdt.junit.JUNIT_CONTAINER/4"} ) == null) {
		node.appendNode('classpathentry', [ exported: false, kind: 'con', path: 'org.eclipse.jdt.junit.JUNIT_CONTAINER/4' ])
	}
}

// Only declared for eclipse
sourceSets {
	main {
		java {
			srcDir 'src/main/java'

			srcDir 'exp/main/java'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
			//srcDir 'src/androidTest/java // Ignore for now

			srcDir 'exp/test/java'
		}
	}
}
