// tokTales-android-library build.gradle
group "com.tokelon.toktales"
version "0.0.1"


apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
	
	packagingOptions {
		exclude 'META-INF/DEPENDENCIES' // Exclude duplicate files from Guice
	}
}


dependencies {
    testImplementation project(':tokTales-android-test')
	androidTestImplementation project(':tokTales-android-test')

	
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
	
	api project(':tokTales-tools-android')
	
    api (project(':tokTales-core-library')) {
	    // Exclude these because of duplicate class XmlPullParserException that Android includes
	    exclude group: 'xmlpull', module: 'xmlpull'
	    exclude group: 'xpp3', module: 'xpp3_min'
	    // We need to exclude the Guice AOP version here because forcing alone (below) does not override the dependency classifier
	    exclude group: 'com.google.inject', module: 'guice'
	    // This is necessary because otherwise projects depending on this, still get the jre version
	    exclude group: 'com.google.guava', module: 'guava'
    }
	api ('com.google.inject:guice:4.2.1:no_aop') {
		// Replace (see above) Guice dependency with no_aop version
		force = true
	}
	api ('com.google.guava:guava:26.0-android') {
		// Force override transient depdencies from Guice and tokTales-core-library with the version we want
		// We have to exclude, declare and force, for this to work; there is no other way
		force true
	}
}


// Tell Gradle eclipse plugin to add dependencies to classpath
eclipse.classpath.file.withXml { xml ->
    def node = xml.asNode()

    node.appendNode('classpathentry', [ exported: false, kind: 'src', path: '/tokTales-android-test' ])
	
	node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-tools-android' ])
	node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-core-library' ])
    
    // Export for dependent projects?
	node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android-library/build/intermediates/compile_only_not_namespaced_r_class_jar/debug/generateDebugRFile/R.jar' ])
	
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-appcompat-v7-classes.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-classes.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-internal.jar' ])
    
    // for some reason there can be a second entry sometimes
    if(node.children().find( {it.@path == "org.eclipse.jdt.junit.JUNIT_CONTAINER/4"} ) == null) {
    	node.appendNode('classpathentry', [ exported: false, kind: 'con', path: 'org.eclipse.jdt.junit.JUNIT_CONTAINER/4' ])
    }
}

// Only declared for eclipse
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			
			srcDir 'exp/main/java'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
			//srcDir 'src/androidTest/java // Ignore for now
			
			srcDir 'exp/test/java'
		}
	}
}
