// tokTales-android-library build.gradle
group "com.tokelon.toktales"
version "0.0.6"


apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.6"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
	
	packagingOptions {
		exclude 'META-INF/DEPENDENCIES' // Exclude duplicate files from Guice
	}
}


dependencies {
    testImplementation project(':tokTales-android-test')
	androidTestImplementation project(':tokTales-android-test')

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'

    api project(':tokTales-core-library')
}


// Tell Gradle eclipse plugin to add dependencies to classpath
eclipse.classpath.file.withXml { xml ->
    def node = xml.asNode()
    
    node.appendNode('classpathentry', [ exported: false, kind: 'src', path: '/tokTales-android-test' ])
    
    node.appendNode('classpathentry', [ exported: true, kind: 'src', path: '/tokTales-core-library' ])
    
	
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-appcompat-v7-classes.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-classes.jar' ])
    node.appendNode('classpathentry', [ exported: false, kind: 'lib', path: '/tokTales-android/libs/android/android-support-v4-internal.jar' ])
    
    // for some reason there can be a second entry sometimes
    if(node.children().find( {it.@path == "org.eclipse.jdt.junit.JUNIT_CONTAINER/4"} ) == null) {
    	node.appendNode('classpathentry', [ exported: false, kind: 'con', path: 'org.eclipse.jdt.junit.JUNIT_CONTAINER/4' ])
    }
}

// Only declared for eclipse - not used by android plugin!
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			
			srcDir '/build/generated/source/r/debug'
			srcDir '/build/generated/not_namespaced_r_class_sources/debug/generateDebugRFile/out'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
			//srcDir 'src/androidTest/java // Ignore for now
		}
	}
}
